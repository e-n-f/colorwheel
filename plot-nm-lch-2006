set terminal postscript

f(x) = a * (1 - (.5 + .5 * erf((x - u) / (sqrt(2) * o)))) + a1 * (1 - (.5 + .5 * erf((x - u1) / (sqrt(2) * o1)))) + a2 * (1 - (.5 + .5 * erf((x - u2) / (sqrt(2) * o2)))) + b

a               = 1.51366   
u               = 574.457   
o               = 29.2535   
a1              = 1.7271    
u1              = 473.458   
o1              = 6.20547   
b               = 0.680437  
a2              = 1.68852   
u2              = 497.363   
o2              = 39.4372   

fit f(x) "nm-lch-2006" using 1:4 via a, u, o, a1, u1, o1, b, a2, u2, o2
plot "nm-lch-2006" using 1:4 with lines, f(x)

set xrange [411:694]
set logscale y
set yrange [.5:200]

# /8 since it looks like the best case for normal vision is ~1nm

g(x) = 1 / ( \
     a  * exp(- (x - u ) ** 2 / (2 * o  ** 2)) / (o  * sqrt(2 * pi)) + \
     a1 * exp(- (x - u1) ** 2 / (2 * o1 ** 2)) / (o1 * sqrt(2 * pi)) + \
     a2 * exp(- (x - u2) ** 2 / (2 * o2 ** 2)) / (o2 * sqrt(2 * pi)) ) / 8;

plot g(x)

ea = a
eu = u
eo = o
ea1 = a1
eu1 = u1
eo1 = o1
ea2 = a2
eu2 = u2
eo2 = o2

eg(x) = 1 / ( \
     ea  * exp(- (x - eu ) ** 2 / (2 * eo  ** 2)) / (eo  * sqrt(2 * pi)) + \
     ea1 * exp(- (x - eu1) ** 2 / (2 * eo1 ** 2)) / (eo1 * sqrt(2 * pi)) + \
     ea2 * exp(- (x - eu2) ** 2 / (2 * eo2 ** 2)) / (eo2 * sqrt(2 * pi)) ) / 8;

fit eg(x) "nm-log" using 1:4 via ea, eu, eo, ea1, eu1, eo1, ea2, eu2, eo2

plot g(x), "nm-log" using 1:4, eg(x)

eg(x) = 1 / ( \
     ea  * exp(- (x - eu ) ** 2 / (2 * eo  ** 2)) / (eo  * sqrt(2 * pi)) + \
     ea1 * exp(- (x - eu1) ** 2 / (2 * eo1 ** 2)) / (eo1 * sqrt(2 * pi)) ) / 8;

fit eg(x) "nm-log" using 1:4 via ea, eu, eo, ea1, eu1, eo1

plot g(x), "nm-log" using 1:4, eg(x)

